EXPORT AI(X)
BEGIN
    RETURN SIN(X)^2+1;
END;

EXPORT AITKEN(p0, tol)
BEGIN

    LOCAL p1, p2, pg, iter;
    LOCAL lastpg, error;

    p1 := AI(p0);
    p2 := AI(p1);
    pg := p0 - ((p1 - p0)^2/(p2 - 2*p1 + p0));
    error := 1000;
    iter := 0;

    REPEAT
        iter := iter + 1;
        lastpg := pg;

        p0 := p1;
        p1 := p2;
        p2 := AI(p1); 
        IF (p2 - 2*p1 + p0) == 0 THEN
            MSGBOX("Division by zero in the formula");
            BREAK;
        ELSE
            pg := p0 - ((p1 - p0)^2/(p2 - 2*p1 + p0));
        END;
        error := ABS(lastpg-pg);
    UNTIL error < tol;

    PRINT("");
    IF error < tol THEN
        PRINT("Converged to: " + STRING(pg, 6));
    ELSE
        PRINT("Did not converge.");
    END;

    RETURN {pg, iter};

END;

EXPORT AITKEN_SPREAD(p0, tol)
BEGIN
    LOCAL p1, p2, pg, iter;
    LOCAL lastpg, error;

    // Header row
    AITKEN.Cell(1, 1) := "Iter";
    AITKEN.Cell(1, 2) := "p0";
    AITKEN.Cell(1, 3) := "p1";
    AITKEN.Cell(1, 4) := "p2";
    AITKEN.Cell(1, 5) := "pg";
    AITKEN.Cell(1, 6) := "Error";

    // Initial values
    p1 := AI(p0);
    p2 := AI(p1);

    IF (p2 - 2*p1 + p0) == 0 THEN
        MSGBOX("Division by zero in first iteration.");
        RETURN "Error";
    END;

    pg := p0 - ((p1 - p0)^2 / (p2 - 2*p1 + p0));
    error := 1000;
    iter := 0;

    // First log BEFORE loop
    AITKEN.Cell(2, 1) := iter;
    AITKEN.Cell(2, 2) := p0;
    AITKEN.Cell(2, 3) := p1;
    AITKEN.Cell(2, 4) := p2;
    AITKEN.Cell(2, 5) := pg;
    AITKEN.Cell(2, 6) := error;

    // Loop
    REPEAT
        iter := iter + 1;
        lastpg := pg;

        p0 := p1;
        p1 := p2;
        p2 := AI(p1);

        IF (p2 - 2*p1 + p0) == 0 THEN
            MSGBOX("Division by zero in the formula during iteration.");
            BREAK;
        END;

        pg := p0 - ((p1 - p0)^2 / (p2 - 2*p1 + p0));
        error := ABS(lastpg - pg);

        AITKEN.Cell(iter+2, 1) := iter;
        AITKEN.Cell(iter+2, 2) := p0;
        AITKEN.Cell(iter+2, 3) := p1;
        AITKEN.Cell(iter+2, 4) := p2;
        AITKEN.Cell(iter+2, 5) := pg;
        AITKEN.Cell(iter+2, 6) := error;

    UNTIL error < tol;

    IF error < tol THEN
        RETURN {pg, iter};
    ELSE
        RETURN "Did not converge";
    END;
END;

